************************************************************************
* Build and Install HDF5 Applications with CMake                       *
************************************************************************

Notes: This short instruction is written for users who want to quickly
       build HDF5 applications using the CMake tools. Users can adapt
       these instructions for their own applications. For more information,
       see the "Minimum C Project Files for CMake" section.

       More information about using CMake can be found at the KitWare
       site, www.cmake.org.

       CMake uses the command line; however, the visual CMake tool is
       available for the configuration step. The steps are similar for
       all of the operating systems supported by CMake.

       NOTES:
         1. Using CMake for building and using HDF5 is under active
            development. While we have attempted to provide error-free
            files, please understand that development with CMake has not
            been extensively tested outside of HDF. The CMake specific
            files may change before the next release.

         2. CMake for HDF5 development should be usable on any system
            where CMake is supported. Please send us any comments on how
            CMake support can be improved on any system.

         3. See the appendix at the bottom of this file for an example
            of using a ctest script for building and testing. See
            INSTALL_CMake.txt for more information.



========================================================================
I. Preconditions
========================================================================

   1. We suggest you obtain the latest CMake for windows from the Kitware
      web site. The HDF5 1.8.x product requires a minimum CMake version
      of 3.2.2.

   2. You have installed the HDF5 library built with CMake, by executing
      the HDF Install Utility (the *.msi file in the binary package for
      Windows). If you are using a Windows platform, you can obtain a
      pre-built Windows binary from The HDF Group's website at
      www.hdfgroup.org.

   3. Set the environment variable HDF5_DIR to the installed location of
      the config files for HDF5. On Windows:
            HDF5_DIR=C:/Program Files/HDF_Group/HDF5/1.8.x/cmake

      (Note there are no quote characters used on Windows and all platforms
      use forward slashes)

   4. Created separate source and build directories.
      (CMake commands are executed in the build directory)

   5. Created a CMakeLists.txt file(s) for your source. See Section III
      below.



========================================================================
II. Building HDF5 Applications with CMake
========================================================================

Go through these steps to build HDF5 applications with CMake.
   (The application must support building with CMake.)

   1. Run CMake
   2. Configure the cache settings
   3. Build HDF5 Applications
   4. Test HDF5 Applications

These steps are described in more detail below.



   1. Run CMake

      The visual CMake executable is named "cmake-gui.exe" on Windows and should be
      available in your Start menu. For Linux, UNIX, and Mac users the
      executable is named "cmake-gui" and can be found where CMake was
      installed.

      Specify the source and build directories. Make the build and source
      directories different. For example on Windows, if the source is at
      c:\MyHDFstuff\hdf5, then use c:\MyHDFstuff\hdf5\build or
      c:\MyHDFstuff\build\hdf5 for the build directory.

      PREFERRED:
        Users can perform the configuration step without using the visual
        cmake-gui program. The following is an example command line
        configuration step executed within the build directory:

        cmake -G "<generator>"  [-D<options>]  <sourcepath>

        Where <generator> is
            * MinGW Makefiles
            * NMake Makefiles
            * Unix Makefiles
            * Visual Studio 12 2013
            * Visual Studio 12 2013 Win64
            * Visual Studio 11 2012
            * Visual Studio 11 2012 Win64
            * Visual Studio 14 2015
            * Visual Studio 14 2015 Win64

        <options> is:
            * BUILD_TESTING:BOOL=ON
            * BUILD_SHARED_LIBS:BOOL=[ON | OFF]

   2. Configure the cache settings

      2.1  Visual CMake users, click the Configure button. If this is the first time you are
           running cmake-gui in this directory, you will be prompted for the
           generator you wish to use (for example on Windows, Visual Studio 12 2013).
           CMake will read in the CMakeLists.txt files from the source directory and
           display options for the HDF5 project. After the first configure you
           can adjust the cache settings and/or specify locations of other programs.

           Any conflicts or new values will be highlighted by the configure
           process in red. Once you are happy with all the settings and there are no
           more values in red, click the Generate button to produce the appropriate
           build files.

           On Windows, if you are using a Visual Studio generator, the solution and
           project files will be created in the build folder.

           On linux, if you are using the Unix Makefiles generator, the Makefiles will
           be created in the build folder.

      2.2  Alternative command line example on Windows in c:\MyHDFstuff\hdf5\build directory:

           cmake -G "Visual Studio 12 2013" -DBUILD_TESTING:BOOL=ON ..

   3. Build HDF5 Applications

      On Windows, you can build HDF5 applications using either the Visual Studio Environment
      or the command line. The command line is normally used on linux, Unix, and Mac.

      To build from the command line, navigate to your build directory and
      execute the following:

              cmake --build . --config {Debug | Release}

      NOTE: "--config {Debug | Release}" may be optional on your platform. We
            recommend choosing either Debug or Release on Windows. If you are
            using the pre-built binaries from HDF, use Release.

      3.1  If you wish to use the Visual Studio environment, open the solution
           file in your build directory. Be sure to select either Debug or
           Release and build the solution.

   4. Test HDF5 Applications

      To test the build, navigate to your build directory and execute:

              ctest . -C {Debug | Release}

      NOTE: "-C {Debug | Release}" may be optional on your platform. We
            recommend choosing either Debug or Release to match the build
            step on Windows.

   5. The files that support building with CMake are all of the files in the
      config/cmake folder, the CMakeLists.txt files in each source folder, and
      CTestConfig.cmake. CTestConfig.cmake is specific to the internal testing
      performed by The HDF Group. It should be altered for the user's
      installation and needs. The cacheinit.cmake file settings are used by
      The HDF Group for daily testing. It should be altered/ignored for the user's
      installation and needs.



========================================================================
III. Minimum C Project Files for CMake
========================================================================

Given the preconditions in section I, create a CMakeLists.txt file at the
source root. Include the following text in the file:

##########################################################
cmake_minimum_required (VERSION 3.2.2)
project (HDF5MyApp C CXX)

set (LIB_TYPE STATIC) # or SHARED
string(TOLOWER ${LIB_TYPE} SEARCH_TYPE)

find_package (HDF5 NAMES hdf5 COMPONENTS C ${SEARCH_TYPE})
# find_package (HDF5) # Find non-cmake built HDF5
INCLUDE_DIRECTORIES (${HDF5_INCLUDE_DIR})
set (LINK_LIBS ${LINK_LIBS} ${HDF5_C_${LIB_TYPE}_LIBRARY})

set (example hdf_example)

add_executable (${example} ${PROJECT_SOURCE_DIR}/${example}.c)
TARGET_NAMING (${example} ${LIB_TYPE})
TARGET_C_PROPERTIES (${example} ${LIB_TYPE} " " " ")
target_link_libraries (${example} ${LINK_LIBS})

enable_testing ()
include (CTest)

add_test (NAME test_example COMMAND ${example})
##########################################################



========================================================================
IV. APPENDIX
========================================================================

Below is an example of a ctest script that can be used to build the examples.
Adjust the values as necessary. Note that the defaults can be entered on the
command line and the build folder is created as a sub-folder. Windows should
adjust the forward slash to double backslashes, except for the HDF_DIR
environment variable.

NOTE: this file is available at the HDF web site:
    http://www.hdfgroup.org/HDF5/release/cmakebuild.html

    HDF518_Examples.cmake

Also available at the HDF web site is a CMake application framework template.
You can quickly add files to the framework and execute the script to compile
your application with an installed HDF5 binary.

========================================================================
ctest
========================================================================

cmake_minimum_required(VERSION 3.2.2 FATAL_ERROR)
###############################################################################################################
# This script will build and run the examples from a folder
# Execute from a command line:
#     ctest -S HDF5_Examples.cmake,OPTION=VALUE -C Release -V -O test.log
###############################################################################################################

set (CTEST_CMAKE_GENERATOR "@CMAKE_GENERATOR@")
set (CTEST_DASHBOARD_ROOT ${CTEST_SCRIPT_DIRECTORY})

# handle input parameters to script.
#INSTALLDIR - HDF5 root folder
#CTEST_CONFIGURATION_TYPE - Release, Debug, RelWithDebInfo
#CTEST_SOURCE_NAME - name of source folder; HDF4Examples
#STATIC_ONLY - Default is YES
#FORTRAN_LIBRARIES - Default is NO
#JAVA_LIBRARIES - Default is NO
##NO_MAC_FORTRAN - set to TRUE to allow shared libs on a Mac)
if (DEFINED CTEST_SCRIPT_ARG)
    # transform ctest script arguments of the form
    # script.ctest,var1=value1,var2=value2
    # to variables with the respective names set to the respective values
    string (REPLACE "," ";" script_args "${CTEST_SCRIPT_ARG}")
    foreach (current_var ${script_args})
        if ("${current_var}" MATCHES "^([^=]+)=(.+)$")
            set("${CMAKE_MATCH_1}" "${CMAKE_MATCH_2}")
        endif ()
    endforeach ()
endif ()
if (NOT DEFINED INSTALLDIR)
    set (INSTALLDIR "@CMAKE_INSTALL_PREFIX@")
endif ()
if (NOT DEFINED CTEST_CONFIGURATION_TYPE)
    set (CTEST_CONFIGURATION_TYPE "Release")
endif ()
if (NOT DEFINED CTEST_SOURCE_NAME)
    set (CTEST_SOURCE_NAME "HDF5Examples")
endif ()
if (NOT DEFINED STATIC_ONLY)
    set (STATICONLYLIBRARIES "YES")
else ()
    set (STATICONLYLIBRARIES "NO")
endif ()
if (NOT DEFINED FORTRAN_LIBRARIES)
    set (FORTRANLIBRARIES "NO")
else ()
    set (FORTRANLIBRARIES "YES")
endif ()
if (NOT DEFINED JAVA_LIBRARIES)
    set (JAVALIBRARIES "NO")
else ()
    set (JAVALIBRARIES "YES")
endif ()

#TAR_SOURCE - name of tarfile
#if (NOT DEFINED TAR_SOURCE)
#  set (CTEST_USE_TAR_SOURCE "HDF5Examples-1.10.1-Source")
#endif ()

###############################################################################################################
#     Adjust the following SET Commands as needed
###############################################################################################################
if (WIN32)
  if (${STATICONLYLIBRARIES})
    set (BUILD_OPTIONS "${BUILD_OPTIONS} -DBUILD_SHARED_LIBS:BOOL=OFF")
  endif ()
  set (ENV{HDF5_DIR} "${INSTALLDIR}/cmake")
  set (CTEST_BINARY_NAME ${CTEST_SOURCE_NAME}\\build)
  set (CTEST_SOURCE_DIRECTORY "${CTEST_DASHBOARD_ROOT}\\${CTEST_SOURCE_NAME}")
  set (CTEST_BINARY_DIRECTORY "${CTEST_DASHBOARD_ROOT}\\${CTEST_BINARY_NAME}")
else (WIN32)
  if (${STATICONLYLIBRARIES})
    set (BUILD_OPTIONS "${BUILD_OPTIONS} -DBUILD_SHARED_LIBS:BOOL=OFF -DCMAKE_ANSI_CFLAGS:STRING=-fPIC")
  endif ()
  set (ENV{HDF5_DIR} "${INSTALLDIR}/share/cmake")
  set (ENV{LD_LIBRARY_PATH} "${INSTALLDIR}/lib")
  set (CTEST_BINARY_NAME ${CTEST_SOURCE_NAME}/build)
  set (CTEST_SOURCE_DIRECTORY "${CTEST_DASHBOARD_ROOT}/${CTEST_SOURCE_NAME}")
  set (CTEST_BINARY_DIRECTORY "${CTEST_DASHBOARD_ROOT}/${CTEST_BINARY_NAME}")
endif(WIN32)
if (${FORTRANLIBRARIES})
  set (BUILD_OPTIONS "${BUILD_OPTIONS} -DHDF_BUILD_FORTRAN:BOOL=ON")
else ()
  set (BUILD_OPTIONS "${BUILD_OPTIONS} -DHDF_BUILD_FORTRAN:BOOL=OFF")
endif ()
if (${JAVALIBRARIES})
  set (BUILD_OPTIONS "${BUILD_OPTIONS} -DHDF_BUILD_JAVA:BOOL=ON")
else ()
  set (BUILD_OPTIONS "${BUILD_OPTIONS} -DHDF_BUILD_JAVA:BOOL=OFF")
endif ()
set (BUILD_OPTIONS "${BUILD_OPTIONS} -DHDF5_PACKAGE_NAME:STRING=@HDF5_PACKAGE@@HDF_PACKAGE_EXT@")

###############################################################################################################
# For any comments please contact cdashhelp@hdfgroup.org
#
###############################################################################################################

#-----------------------------------------------------------------------------
# MAC machines need special option
#-----------------------------------------------------------------------------
if (APPLE)
  # Compiler choice
  execute_process (COMMAND xcrun --find cc OUTPUT_VARIABLE XCODE_CC OUTPUT_STRIP_TRAILING_WHITESPACE)
  execute_process (COMMAND xcrun --find c++ OUTPUT_VARIABLE XCODE_CXX OUTPUT_STRIP_TRAILING_WHITESPACE)
  set (ENV{CC} "${XCODE_CC}")
  set (ENV{CXX} "${XCODE_CXX}")
  if (NOT NO_MAC_FORTRAN)
    # Shared fortran is not supported, build static
    set (BUILD_OPTIONS "${BUILD_OPTIONS} -DBUILD_SHARED_LIBS:BOOL=OFF -DCMAKE_ANSI_CFLAGS:STRING=-fPIC")
  else ()
    set (BUILD_OPTIONS "${BUILD_OPTIONS} -DHDF_BUILD_FORTRAN:BOOL=OFF")
  endif ()
  set (BUILD_OPTIONS "${BUILD_OPTIONS} -DCTEST_USE_LAUNCHERS:BOOL=ON -DCMAKE_BUILD_WITH_INSTALL_RPATH:BOOL=OFF")
endif ()

#-----------------------------------------------------------------------------
set (CTEST_CMAKE_COMMAND "\"${CMAKE_COMMAND}\"")
## --------------------------
if (CTEST_USE_TAR_SOURCE)
  ## Uncompress source if tar or zip file provided
  ## --------------------------
  if (WIN32)
    message (STATUS "extracting... [${CMAKE_EXECUTABLE_NAME} -E tar -xvf ${CTEST_USE_TAR_SOURCE}.zip]")
    execute_process (COMMAND ${CMAKE_EXECUTABLE_NAME} -E tar -xvf ${CTEST_DASHBOARD_ROOT}\\${CTEST_USE_TAR_SOURCE}.zip RESULT_VARIABLE rv)
  else ()
    message (STATUS "extracting... [${CMAKE_EXECUTABLE_NAME} -E tar -xvf ${CTEST_USE_TAR_SOURCE}.tar]")
    execute_process (COMMAND ${CMAKE_EXECUTABLE_NAME} -E tar -xvf ${CTEST_DASHBOARD_ROOT}/${CTEST_USE_TAR_SOURCE}.tar RESULT_VARIABLE rv)
  endif ()

  if (NOT rv EQUAL 0)
    message (STATUS "extracting... [error-(${rv}) clean up]")
    file (REMOVE_RECURSE "${CTEST_SOURCE_DIRECTORY}")
    message (FATAL_ERROR "error: extract of ${CTEST_SOURCE_NAME} failed")
  endif ()
endif ()

#-----------------------------------------------------------------------------
## Clear the build directory
## --------------------------
set (CTEST_START_WITH_EMPTY_BINARY_DIRECTORY TRUE)
if (EXISTS "${CTEST_BINARY_DIRECTORY}" AND IS_DIRECTORY "${CTEST_BINARY_DIRECTORY}")
  ctest_empty_binary_directory (${CTEST_BINARY_DIRECTORY})
else ()
  file (MAKE_DIRECTORY "${CTEST_BINARY_DIRECTORY}")
endif ()

# Use multiple CPU cores to build
include (ProcessorCount)
ProcessorCount (N)
if (NOT N EQUAL 0)
  if (NOT WIN32)
    set (CTEST_BUILD_FLAGS -j${N})
  endif ()
  set (ctest_test_args ${ctest_test_args} PARALLEL_LEVEL ${N})
endif ()
set (CTEST_CONFIGURE_COMMAND
    "${CTEST_CMAKE_COMMAND} -C \"${CTEST_SOURCE_DIRECTORY}/config/cmake/cacheinit.cmake\" -DCMAKE_BUILD_TYPE:STRING=${CTEST_CONFIGURATION_TYPE} ${BUILD_OPTIONS} \"-G${CTEST_CMAKE_GENERATOR}\" \"${CTEST_SOURCE_DIRECTORY}\""
)

#-----------------------------------------------------------------------------
## -- set output to english
set ($ENV{LC_MESSAGES}  "en_EN")

#-----------------------------------------------------------------------------
configure_file (${CTEST_SOURCE_DIRECTORY}/config/cmake/CTestCustom.cmake ${CTEST_BINARY_DIRECTORY}/CTestCustom.cmake)
ctest_read_custom_files ("${CTEST_BINARY_DIRECTORY}")
## NORMAL process
## --------------------------
ctest_start (Experimental)
ctest_configure (BUILD "${CTEST_BINARY_DIRECTORY}")
if (LOCAL_SUBMIT)
  ctest_submit (PARTS Configure Notes)
endif ()
ctest_build (BUILD "${CTEST_BINARY_DIRECTORY}" APPEND)
if (LOCAL_SUBMIT)
  ctest_submit (PARTS Build)
endif ()
ctest_test (BUILD "${CTEST_BINARY_DIRECTORY}" APPEND ${ctest_test_args} RETURN_VALUE res)
if (LOCAL_SUBMIT)
  ctest_submit (PARTS Test)
endif ()
if (res GREATER 0)
  message (FATAL_ERROR "tests FAILED")
endif ()
#-----------------------------------------------------------------------------
##############################################################################################################



========================================================================
For further assistance, send email to help@hdfgroup.org
========================================================================


